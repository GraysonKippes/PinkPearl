#version 460
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_nonuniform_qualifier : require

// This compute shader takes in an array of vectors (representing positions),
// and creates translation matrices. Then each matrix is multiplied by the camera
// and projection matrices, and push to an output buffer.

// TODO - consider moving view and projection matrix computation to CPU.

#define MAX_MODEL_COUNT 256

// Type/struct definitions

// Keeps track of current and previous vectors for interpolation.
struct RenderVector {
	vec4 current;
	vec4 previous;
};

struct RenderTransform {
	RenderVector translation;
	RenderVector scaling;
	RenderVector rotation;
};

struct ProjectionBounds {
	float left;
	float right;
	float bottom;
	float top;
	float near;
	float far;
};

// Shader layout defintions

// TODO: maybe use local_size_y to compute matrices for multiple matrix buffers.
layout(local_size_x = MAX_MODEL_COUNT) in;

layout(set = 0, binding = 0) uniform sampler samplers[];

layout(set = 0, binding = 1) uniform texture2DArray sampledImages[];

layout(set = 0, binding = 2, rgba8ui) uniform uimage2DArray storageImages[];

// TODO: make scalar
layout(set = 0, binding = 3, std140) uniform TransformBuffers {
	ProjectionBounds projectionBounds;
	float deltaTime;
	vec4 cameraPosition;
	RenderTransform transforms[MAX_MODEL_COUNT];
} transformBuffers[];

// TODO: make scalar
layout(set = 0, binding = 4, std140) writeonly buffer MatrixBuffers {
	mat4 viewMatrix;
	mat4 projectionMatrix;
	mat4 modelMatrices[MAX_MODEL_COUNT];
} matrixBuffers[];

layout(push_constant) uniform PushConstants {
	uint transformBufferIndex;
	uint matrixBufferIndex;
} pushConstants;

// Function definitions

vec4 vec4Lerp(const RenderVector vector, const float deltaTime) {
	return vec4(
		mix(vector.previous.x, vector.current.x, deltaTime),
		mix(vector.previous.y, vector.current.y, deltaTime),
		mix(vector.previous.z, vector.current.z, deltaTime),
		mix(vector.previous.w, vector.current.w, deltaTime)
	);
}

mat4 makeTranslationMat4(const vec4 position) {
	return mat4(
		1.0, 0.0, 0.0, 0.0,
		0.0, 1.0, 0.0, 0.0,
		0.0, 0.0, 1.0, 0.0,
		position.xyz,  1.0
	);
}

// Computes a view matrix given a camera position.
mat4 makeViewMatrix(const vec4 cameraPosition) {
	return mat4(
		1.0, 0.0, 0.0, 0.0,
		0.0, 1.0, 0.0, 0.0,
		0.0, 0.0, 1.0, 0.0,
		-cameraPosition.xyz, 1.0
	);
}

// Makes and returns an orthographic projection matrix.
// TODO - make this just take width, height, length, etc., instead of bounds.
mat4 makeOrthographicProjectionMatrix(ProjectionBounds bounds) {
	
	const float zoomFactor = 1.0;
	
	const float scaleX = (2.0 * zoomFactor) / (bounds.right - bounds.left);
	const float scaleY = -(2.0 * zoomFactor) / (bounds.bottom - bounds.top);
	const float scaleZ = -(2.0 * zoomFactor) / (bounds.far - bounds.near);

	const float midX = (bounds.right + bounds.left) / (bounds.right - bounds.left);
	const float midY = (bounds.top + bounds.bottom) / (bounds.top - bounds.bottom);
	const float midZ = (bounds.far + bounds.near) / (bounds.far - bounds.near);

	return mat4(
		scaleX, 0.0, 0.0, 0.0,
		0.0, scaleY, 0.0, 0.0,
		0.0, 0.0, scaleZ, 0.0,
		-midX, -midY, -midZ, 1.0
	);
}

void main() {

	// Index into global array of model, loaded or unloaded.
	const uint modelIndex = gl_LocalInvocationID.x;
	
	// Transform info.
	const ProjectionBounds projectionBounds = transformBuffers[pushConstants.transformBufferIndex].projectionBounds;
	const float deltaTime = transformBuffers[pushConstants.transformBufferIndex].deltaTime;
	const vec4 cameraPosition = transformBuffers[pushConstants.transformBufferIndex].cameraPosition;
	const RenderVector translation = transformBuffers[pushConstants.transformBufferIndex].transforms[modelIndex].translation;
	
	// Compute matrices.
	matrixBuffers[pushConstants.matrixBufferIndex].modelMatrices[modelIndex] = makeTranslationMat4(vec4Lerp(translation, deltaTime));
	matrixBuffers[pushConstants.matrixBufferIndex].viewMatrix = makeViewMatrix(cameraPosition);
	matrixBuffers[pushConstants.matrixBufferIndex].projectionMatrix = makeOrthographicProjectionMatrix(projectionBounds);
}
