#version 450

// This compute shader takes in an array of vectors (representing positions),
// and creates translation matrices. Then each matrix is multiplied by the camera
// and projection matrices, and push to an output buffer.

#define MAX_SIZE_X 64

layout(local_size_x = MAX_SIZE_X) in;

// TODO - specify std140 memory layout.
layout(binding = 0) uniform config {

	uint num_inputs;

	float delta_time;

	vec3 camera_position;
	vec3 camera_previous_position;

	// TODO - make this a struct for better memory packing.
	float projection_left;
	float projection_right;
	float projection_bottom;
	float projection_top;
	float projection_near;
	float projection_far;

} op_data;

// TODO - this should probably be a uniform buffer.
layout(binding = 1) readonly buffer input_buffer {
	vec3 positions[MAX_SIZE_X];
	vec3 previous_positions[MAX_SIZE_X];
} input_data;

layout(binding = 2) writeonly buffer output_buffer {
	mat4 matrices[MAX_SIZE_X];
} output_data;

// Makes and returns a "translerp" (portmanteau of "translate" and "lerp") matrix.
mat4 make_translerp_matrix(vec3 position, vec3 previous_position, float delta_time) {

	vec3 lerp_position = vec3(
		mix(previous_position.x, position.x, delta_time),
		mix(previous_position.y, position.y, delta_time),
		mix(previous_position.z, position.z, delta_time));
	
	
	mat4 translation_matrix;
	translation_matrix[0] = vec4(1.0, 0.0, 0.0, 0.0);
	translation_matrix[1] = vec4(0.0, 1.0, 0.0, 0.0);
	translation_matrix[2] = vec4(0.0, 0.0, 1.0, 0.0);
	translation_matrix[3] = vec4(lerp_position, 1.0);

	return translation_matrix;
}

// Makes and returns an orthographic projection matrix.
mat4 make_orthographic_projection_matrix(float left, float right, float bottom, float top, float near, float far) {
	
	float scale_x = 2.0 / (right - left);
	float scale_y = 2.0 / (top - bottom);
	float scale_z = -2.0 / (far - near);

	float mid_x = (right + left) / (right - left);
	float mid_y = (top + bottom) / (top - bottom);
	float mid_z = (far + near) / (far - near);

	mat4 projection_matrix;
	projection_matrix[0] = vec4(scale_x, 0.0, 0.0, 0.0);
	projection_matrix[1] = vec4(0.0, scale_y, 0.0, 0.0);
	projection_matrix[2] = vec4(0.0, 0.0, scale_z, 0.0);
	projection_matrix[3] = vec4(-mid_x, -mid_y, -mid_z, 1.0);

	return projection_matrix;
}

void main() {
	
	uint global_ID = gl_GlobalInvocationID.x;

	mat4 camera_matrix = make_translerp_matrix(op_data.camera_position, op_data.camera_previous_position, op_data.delta_time);

	mat4 projection_matrix = make_orthographic_projection_matrix(op_data.projection_left, op_data.projection_right, op_data.projection_bottom, op_data.projection_top, op_data.projection_near, op_data.projection_far);

	if (global_ID < op_data.num_inputs) {
		mat4 model_matrix = make_translerp_matrix(input_data.positions[global_ID], input_data.previous_positions[global_ID], op_data.delta_time);
		output_data.matrices[global_ID] = projection_matrix * camera_matrix * model_matrix;
	}
}
